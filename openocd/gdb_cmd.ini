##
## Filename:
## gdb_cmd.ini
##
## Description:
## GDB macro definitions for an AT91Bootstrap/application
## debug session using the JTAG debugger Olimex ARM-USB-OCD.
##
## Usage:
## 1) Start OpenOCD
##    openocd --file ./openocd.cfg
##
## 2) Start GDB
##    arm-eabi-gdb --command=./gdb_cmd.ini <path_to_ELF_to_debug>
##
##    <path_to_ELF_to_debug>
##    Path to ELF to debug (AT91Bootstrap or final application).
##
##################################################################
# Configuration
##################################################################

### NandFlash

# AT91Bootstrap, NandFlash address & size
set $AT91_FLASH_ADDR = 0x00000000
echo AT91Bootstrap Flash address :
print/x $AT91_FLASH_ADDR

# final (bare-metal) application NandFlash address
set $APP_FLASH_ADDR = 0x00020000
echo Application Flash address   :
print/x $APP_FLASH_ADDR

### SRAM / SDRAM

# AT91Bootstrap link address (SRAM)
set $AT91_LINK_ADDR = 0x200000
echo AT91Bootstrap link address(SRAM) :
print/x $AT91_LINK_ADDR

# initial SRAM hw-breakpoint in AT91Bootstrap,
# bypassing the load of U-Boot from NandFlash to SDRAM
set $AT91_BREAK_ADDR = 0x2001d8
echo AT91Bootstrap hw-breakpoint(SRAM):
print/x $AT91_BREAK_ADDR

# final (bare-metal) application link address (SDRAM)
set $APP_LINK_ADDR = 0x22200000
echo Application link address(SDRAM)  :
print/x $APP_LINK_ADDR

##################################################################
# Prepare the application debug session.
#  -connect to OpenOCD
#  -setup JTAG
##################################################################
define init_ocd
  echo >>>> init JTAG\n 
  target remote localhost:1972
  mon init
  mon reset halt
  mon reset init
  eval "mon rbp 0x%x", $AT91_BREAK_ADDR
  mon halt
  mon arm7_9 fast_memory_access enable
  mon jtag_khz 1000
end

document init_ocd
Setup JTAG debugger Olimex ARM-USB-OCD for debug session
end

##################################################################
# Update AT91Bootstrap (BIN-->NandFlash)
#  - address=0x0000_0000, max-length=4KB (0x1000)
##################################################################
define update_at91
  # prepare debug session
  init_ocd
  # check NandFlash
  echo >>>> update flash\n
  mon nand list
  mon nand probe 0
  # enable
  mon nand raw_access 0 enable
  # erase one block (128KB=0x20000) at AT91Bootstrap start
  eval "mon nand erase 0 0x%x 0x20000", $AT91_FLASH_ADDR
  # write binary data
  eval "mon nand write 0 ../fw/sam9_l9260_hbr-nandflashboot-2.4.bin 0x%x", $AT91_FLASH_ADDR
  # verify
  eval "mon nand verify 0 ../fw/sam9_l9260_hbr-nandflashboot-2.4.bin 0x%x", $AT91_FLASH_ADDR
  # disable
  mon nand raw_access 0 disable
end

document update_at91
Update AT91Bootstrap in NandFlash
end

##################################################################
# Update final application (BIN-->NandFlash)
#  - address=0x0002_0000, max-length=128KB (0x20000)
##################################################################
define update_app
  # prepare debug session
  init_ocd
  # check NandFlash
  echo >>>> update flash\n
  mon nand list
  mon nand probe 0
  # enable
  mon nand raw_access 0 enable
  # erase one block (128KB=0x20000) at application start
  eval "mon nand erase 0 0x%x 0x20000", $APP_FLASH_ADDR
  # write binary data
  eval "mon nand write 0 ../fw/mpc.bin 0x%x", $APP_FLASH_ADDR
  # verify
  eval "mon nand verify 0 ../fw/mpc.bin 0x%x", $APP_FLASH_ADDR
  # disable
  mon nand raw_access 0 disable
end

document update_app
Update final application in NandFlash
end

##################################################################
# Debug AT91Bootstrap (ELF-->SRAM)
#  -load at SRAM address specified in ELF
#  -set initial breakpoints in AT91Bootstrap
#  -start execution of AT91Bootstrap
##################################################################
define debug_at91
  # prepare debug session
  init_ocd
  # load AT91Bootstrap
  echo >>>> load ELF\n
  load
  # set initial breakpoints
  echo >>>> define breakpoints\n
  b main
  b hw_init
  # peek at first ten lines of code
  mon reg pc
  eval "mon arm disassemble 0x%x 10", $AT91_LINK_ADDR
  # start AT91Bootstrap (break at main)
  echo >>>> start AT91Bootstrap\n
  c
end

document debug_at91
Debug AT91Bootstrap from SRAM
end

##################################################################
# Load and start AT91Bootstrap (BIN-->SRAM)
#  -load binary image to SRAM
#  -set initial hw-breakpoint in AT91Bootstrap
#  -start execution of AT91Bootstrap
##################################################################
define start_at91
  # prepare debug session
  init_ocd
  # load AT91Bootstrap image to SRAM
  echo >>>> load AT91Bootstrap to SRAM\n
  eval "mon load_image ../fw/sam9_l9260_hbr-nandflashboot-2.4.bin 0x%x bin", $AT91_LINK_ADDR
  # set initial breakpoint
  eval "mon bp 0x%x 4", $AT91_BREAK_ADDR
  # start AT91Bootstrap (break at hw-breakpoint)
  echo >>>> start AT91Bootstrap\n
  eval "mon resume 0x%x", $AT91_LINK_ADDR
end

document start_at91
Load and start AT91Bootstrap from SRAM
end

##################################################################
# Debug final application (ELF-->SDRAM)
#  -prepare debug session (setup hardware)
#  -load at SDRAM address specified in ELF
#  -set initial breakpoint in application
#  -start execution of application
##################################################################
define debug_app
  # prepare debug session and start AT91Bootstrap
  start_at91
  # allow target to stop
  mon sleep 5000
  # remove any initial breakpoint in AT91Bootstrap
  eval "mon rbp 0x%x", $AT91_BREAK_ADDR
  # load application
  echo >>>> load ELF\n
  load
  # set initial breakpoint
  echo >>>> define breakpoints\n
  b c_main
  # peek at first ten lines of code
  mon reg pc
  eval "mon arm disassemble 0x%x 10", $APP_LINK_ADDR
  # start application (break at main)
  echo >>>> start application\n
  c
end

document debug_app
Debug final application from SDRAM
end

##################################################################
# Startup system from NandFlash
#  - ROM Boot
#  - AT91Bootstrap
#  - Final application
##################################################################
define start_system
  # prepare debug session
  init_ocd
  # startup system
  echo >>>> startup\n
  mon reset run
end

document start_system
Start system from NandFlash
end
