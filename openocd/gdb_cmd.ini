##
## Filename:
## gdb_cmd.ini
##
## Description:
## GDB macro definitions for an AT91Bootstrap/application
## debug session using the JTAG debugger Olimex ARM-USB-OCD.
##
## Usage:
## 1) Start OpenOCD
##    openocd --file ./openocd.cfg
##
## 2) Start GDB
##    arm-eabi-gdb \
##      ../sw/bare_metal_test_irq/obj/bmti.elf \
##      -command=./gdb_cmd.ini 
##

##################################################################
# Prepare the application debug session.
#  -connect to OpenOCD, setup JTAG
#  -set initial breakpoint in AT91Bootstrap
#  -start execution of AT91Bootstrap
##################################################################
define init_ocd
  target remote localhost:1972
  mon init
  mon reset halt
  mon reset init
  mon rbp 0x200344
  mon arm7_9 fast_memory_access enable
  mon jtag_khz 1000
  mon load_image ../fw/sam9_l9260_hbr-nandflashboot-2.4.bin 0x200000 bin
  mon bp 0x200344 4
  mon resume 0x200000
end

document init_ocd
Setup JTAG debugger Olimex ARM-USB-OCD for debug session
end

##################################################################
# Load and start actual application
##################################################################
define start_app
  mon rbp 0x200344
  # load application
  load
  b c_main
  # peek at first lines of code
  mon reg pc
  mon arm disassemble 0x22200000 10
  # start application
  c
end

document start_app
Load and start application
end
